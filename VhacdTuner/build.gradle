// Gradle script to build the Minie/VhacdTuner sub-project

// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application' // to build JVM applications
}

tasks.withType(JavaExec).configureEach { // Java runtime options:
    args '--openGL3'
}

application {
    mainClass = 'jme3utilities.minie.tuner.VhacdTuner'
}
if (!hasProperty('mainClass')) {
    ext.mainClass = application.mainClass
}
jar.manifest.attributes('Main-Class': application.mainClass)

dependencies {
    runtimeOnly    desktopCoordinates
    implementation heartCoordinates
    runtimeOnly    lwjglCoordinates
    implementation niftyCoordinates
    runtimeOnly    pluginsCoordinates
    runtimeOnly    styleCoordinates

    // VhacdTuner doesn't use jme3-jogg
    //  -- it is included solely to avoid warnings from AssetConfig.
    runtimeOnly    joggCoordinates

    //implementation 'com.github.stephengold:Minie:' + minieVersion // for published library
    implementation project(':MinieLibrary') // for latest sourcecode
}

startScripts.dependsOn(':MinieLibrary:assemble')
distTar.dependsOn(':MinieLibrary:assemble')
distZip.dependsOn(':MinieLibrary:assemble')

tasks.register('runForceDialog', JavaExec) {
    args '--forceDialog'
    mainClass = application.mainClass
}
