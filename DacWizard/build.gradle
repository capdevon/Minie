// Gradle script to build the DacWizard subproject of Minie

// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application' // to build JVM applications
}

tasks.withType(JavaExec).configureEach { // Java runtime options:
    args '--openGL3'
}

application {
    mainClass = 'jme3utilities.minie.wizard.DacWizard'
}
if (!hasProperty('mainClass')) {
    ext.mainClass = application.mainClass
}
jar.manifest.attributes('Main-Class': application.mainClass)

dependencies {
    runtimeOnly(libs.jme3.desktop)
    implementation(libs.heart)
    runtimeOnly(libs.lwjgl)
    implementation(libs.jme3.utilities.nifty)
    runtimeOnly(libs.jme3.plugins)
    runtimeOnly(libs.nifty.style.black)
    implementation(libs.wes)

    // DacWizard doesn't use jme3-jogg
    //  -- it is included solely to avoid warnings from AssetConfig.
    runtimeOnly(libs.jme3.jogg)

    //implementation 'com.github.stephengold:Minie:' + minieVersion // for published library
    implementation project(':MinieLibrary') // for latest sourcecode
}

tasks.register('runForceDialog', JavaExec) {
    args '--forceDialog'
    mainClass = application.mainClass
}
